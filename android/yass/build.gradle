apply plugin: 'com.android.application'

android {
    // https://developer.android.com/build/configure-app-module#set-namespace
    namespace = "it.gui.yass"
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId 'it.gui.yass'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName '1.0'
    }
    sourceSets {
        main {
            assets.srcDirs = ['assets']
            jni.srcDirs = [] // disable automatic ndk-build call
            jniLibs.srcDirs = ['libs'] // add your jni .so file path
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // https://developer.android.com/build/configure-apk-splits
    splits {
        abi {
            enable true
            // Resets the list of ABIs for Gradle to create APKs for to none.
            reset()
            // Specifies a list of ABIs for Gradle to create APKs for.
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            // Specifies that you don't want to also generate a universal APK that includes all ABIs.
            universalApk false
        }
    }
}

task copyTask {
    copy {
        from '../common/res/drawable'
        into 'src/main/res/drawable'
        include 'icon.png'
    }
    copy {
        from '../../third_party/icu/android'
        into 'assets'
        include 'icudtl.dat'
    }
    copy {
        from '../../third_party/imgui/misc/fonts'
        into 'assets'
        include 'DroidSans.ttf'
    }
    copy {
        from '../../build-android24-x86/libyass.so'
        into 'libs/x86'
        rename('libyass.so', 'libnative-lib.so')
    }
    copy {
        from '../../build-android24-x64/libyass.so'
        into 'libs/x86_64'
        rename('libyass.so', 'libnative-lib.so')
    }
    copy {
        from '../../build-android24-arm/libyass.so'
        into 'libs/armeabi-v7a'
        rename('libyass.so', 'libnative-lib.so')
    }
    copy {
        from '../../build-android24-arm64/libyass.so'
        into 'libs/arm64-v8a'
        rename('libyass.so', 'libnative-lib.so')
    }
}

preBuild.dependsOn copyTask
